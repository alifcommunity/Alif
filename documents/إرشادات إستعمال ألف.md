<br>

<center style="font-family: Tajawal">

# بسم الله الرحمن الرحيم

</center>

---

<right style = "font-family: 'Tajawal', Tajawal; direction: rtl">

<br>

في هذا الملف سيتم شرح طريقة عمل لغة ألف5 بإختصار وباستخدام الأمثلة.

<br>

---

يمكن تشغيل البرنامج المكتوب بلغة ألف5 بإحدى طريقتين:

- من خلال تمرير كلمة alif5 في الطرفية، حيث ستبدأ الطرفية بإستقبال الشفرة لتنفيذها بشكل فوري - حيث سيتم تنفيذ العمليات البسيطة فقط وهي العمليات أحادية السطر -
- أو تمرير كلمة alif5 بالإضافة إلى اسم الملف test.alif5 حيث سيتم تنفيذ الشفرة المكتوبة في الملف test



	###### *يمكن الخروج من البرنامج اثناء استخدام الطرفية عن طريق كتابة "خروج"*
	###### *الطرفية تستطيع تشغيل العمليات من 1 حتى 8 والتي تسمى بالعمليات البسيطة وهي مكونة من سطر واحد أما باقي العمليات فهي عمليات مركبة والتي تتكون من عدة اسطر ولا يمكن تشغيلها إلا من ملف*

---

<br>

### العمليات التي تستطيع اللغة تنفيذها:

1. التعليقات التوضيحية"#":
    ```
	س = 9 #هذا تعليق يوضح عمل الشفرة المكتوبة
	# تعليق في سطر فارغ
	```

2. العمليات الحسابية:  
   ```
   جمع: 1+2  = 3
   طرح: 9-3  = 6
   ضرب: 2*8  = 16
   قسمة: 9\3  = 3
   باقي: 4\\3  = 1
   أولوية: (3+6)*2  = 18

   1 + 2 - 3 * 4 \ 5 + (2^3)  = 8.6
   ```

3. المقارنة:
    ```
	9 == 9  = 1 أي صح
	9 != 9  = 0 أي خطأ
	3 > 6  = 0 أي خطأ
	6 < 9  = 1 أي صح
	6 >= 6  = 1 أي صح
	9 <= 3  = 0 أي خطأ
	صح == صح  = 1 أي صح
	...
	```
	
4. العمليات المنطقية:
    ```
	صح و صح  = 1 أي صح
	صح و خطا  = 0 أي خطأ
	خطا و خطا  = 0 أي خطأ
	صح او صح  = 1 أي صح
	صح او خطا  = 1 أي صح
	خطا او خطا  = 0 أي خطأ
	ليس خطا  = 1 أي صح
	ليس صح  = 0 أي خطأ
	```

5. الإسناد الشرطي:
    ```
	9 اذا 6 == 6 والا 7  => 9
	```
	حيث يتم اختيار الرقم 9 اذا تحقق الشرط وهو "6 == 6". 
	وفي حال لم يتحقق الشرط يتم اختيار الرقم 7.

6. مصفوفة:  
    ```
	1, 2+3 , "السلام عليكم", [7,6]  = [1 , 5, السلام عليكم,[6 ,7]]
	[1,2,"3",[4,5]]  = [1,2,"3",[4,5]]
	```

7. المتغير:
    ```
	س = 10
	ص = 9 اذا س > 6 والا 3
	ص += س
	```

8. الدوال الضمنية:
    ```
	ادخل()
	اطبع()
	```
	الدوال الضمنية هي مجموعة من الدوال المضمنة في اللغة نفسها، ومع ذلك من الممكن كتابة دالة فوقها.  
	مثال:
	```
	اطبع(س)
	```
	في هذا المثال سيتم تخزين المدخلات كنصوص ومن ثم طباعتها.  
	ولكن كما ذكرنا يمكن عمل دالة باسم دالة اطبع وبالتالي ستتغير وظيفة اطبع.

9. حالة "اذا" الشرطية:
    ```
	س = 6
	اذا س > 9:
    	اطبع("س أكبر من 9")
	واذا س == 9:
    	اطبع("س تساوي 9") 
	والا:
    	اطبع("افعل شيء ما")
	```

10. حالة "لاجل":

    ```
	لاجل ب في (10):
    	اطبع("افعل شيء ما")
	```
	في هذا المثال يتم عمل حلقة "لاجل" لتنفيذ برنامج يطبع على الشاشة جملة "افعل شيئاً ما" عشر مرات.  
	حيث تبدأ الحلقة بالعدد 0 وتقوم بإسناده إلى المتغير ب ومن ثم تنفذ جسم الحلقة.  
	يتكرر الأمر حتى وصول الحلقة إلى العدد 9 - وهو العاشر لأن الحلقة بدأت من 0 - وبالتالي يتوقف التنفيذ.
    ```
    لاجل ب في (2,100,20):
    	اطبع("افعل شيء ما")
	```
	سيتم إسناد 20 إلى المتغير ب ومن ثم يقوم بتنفيذ الحلقة.  
	بعد الإنتهاء سيتم إسناد 22 إلى المتغير ب وذلك لأن عدد التزايد المحدد هو 2.   
	وهكذا حتى يصل المتغير ب الى العدد 100.  

11. حالة "بينما":
    ```
	س = 10
	بينما س > 0:
    	س -= 1
		اطبع("افعل شيء ما")
	```
	سيتم التحقق من الشرط (س > 0) ، إذا كان صحيحاً سيتم تنفيذ جسم الحالة.  

12. تعريف دالة:
    ```
	دالة التحية():
    	اطبع("السلام عليكم ورحمة الله")

	التحية()
	```
	في هذا المثال تم عرض طريقة تعريف دالة بالإضافة إلى دالة اطبع() المضمنة في اللغة.
	
	```
	دالة السرعة(المسافة, الزمن = 1):
        ارجع المسافة \ الزمن

	سرعة_سقوط_الكرة = السرعة(10, 5)

	اطبع(سرعة_سقوط_الكرة)
	```
	في هذا المثال تم توضيح تعريف الدالة مع تمرير معاملان أحدهما لا يمتلك قيمة افتراضية والآخر يمتلك قيمة افتراضية.  
	كما أنه تم توضيح طريقة إرجاع قيمة من الدالة.  
	والمثال عبارة عن دالة لحساب السرعة في حال تحديد قيمة المسافة المقطوعة والزمن.  
	وفي حال عدم تحديد قيمة الزمن فإن الدالة ستقوم بإستخدام القيمة الإفتراضية الممررة.

13. تعريف صنف: 
    ```

	صنف مخروط_علوي:
		نق_القاعدة = 0
		الارتفاع = 0
		بي = 0

		دالة تهيئة(نق = 1, ع = 1):	
			مخروط_علوي.نق_القاعدة = نق
			مخروط_علوي.الارتفاع = ع
			مخروط_علوي.بي = 3.14
	
		دالة مساحة_القاعدة():
			م_القاعدة = 2 * مخروط_علوي.بي * مخروط_علوي.نق_القاعدة * مخروط_علوي.الارتفاع
			ارجع م_القاعدة
	
		دالة حجم_المخروط():
			ارجع مخروط_علوي.مساحة_القاعدة() * مخروط_علوي.الارتفاع \ 3

	صنف مخروط_سفلي:
		نق_القاعدة = 0
		الارتفاع = 0
		بي = 0
		مخروط_ع = مخروط_علوي()

		دالة تهيئة(نق = 1, ع = 1):	
			مخروط_سفلي.نق_القاعدة = نق
			مخروط_سفلي.الارتفاع = ع
			مخروط_سفلي.بي = 3.14
			مخروط_ع.تهيئة(2, 2)
	
		دالة مساحة_القاعدة():
			م_القاعدة = 2 * مخروط_سفلي.بي * مخروط_سفلي.نق_القاعدة * مخروط_سفلي.الارتفاع
			ارجع م_القاعدة
	
		دالة حجم_المخروط():
			ارجع مخروط_سفلي.مساحة_القاعدة() * مخروط_سفلي.الارتفاع \ 3

		دالة مح_المخروطين():
			ارجع مخروط_سفلي.حجم_المخروط() + مخروط_ع.حجم_المخروط()

	```
	في هذا المثال تم تعريف صنف من نوع مخروط وتم إدراج المتغيرات مع الدوال الخاصة بالصنف.

	```
	مخروط_أ = مخروط_سفلي()
	مخروط_ب = مخروط_سفلي()

	مخروط_أ.تهيئة(3, 4)
	ح_المخاريط_أ = مخروط_أ.مح_المخروطين()

	مخروط_ب.تهيئة(5, 4)
	ح_المخاريط_ب = مخروط_ب.مح_المخروطين()

	اطبع([ح_المخاريط_أ, ح_المخاريط_ب])
	```  

	######	ملاحظة: لم يتم عمل نظام الوراثة بعد ,يوجد مشكلة في نظام النطاقات في حالة صنف. 
</right>  


<center>  
<br>
<br>
<br>

:zap: ـــــــــــــــــــــــــــــــــــــــــــــــــــــــــــــــــــــــــــــــــــــــــــــــــــــــــــــــــــــــــــــــــــــــــــــــــــــــــــــــــــــــــــ :zap:

<br>
<br>
<br>
</center>

| ملاحظات: |
| :---: |
| محدد الخطأ غير مكتمل، وقد يتم تحديد مكان الخطأ بدقة أو تظهر رسالة توضح أنه يوجد خطأ وقد يتم الخروج من البرنامج دون معرفة السبب أو الخطأ. |
| لغة ألف5 تقوم بإدارة الذاكرة بشكل تلقائي، ولكن يفضل عدم معالجة كميات ضخمة جداً من البيانات أو الدخول في حلقات لا نهائية وذلك تفادياً لأي ضرر قد يحصل لجهازك. |
