<br>

## بسم الله الرحمن الرحيم


---


في هذا الملف سيتم شرح طريقة عمل لغة ألف5 بإختصار وباستخدام الأمثلة.


---

يمكن تشغيل البرنامج المكتوب بلغة ألف5 بإحدى طريقتين:

- من خلال تمرير كلمة alif في الطرفية، حيث ستبدأ الطرفية بإستقبال الشفرة لتنفيذها بشكل فوري "غير متاحة في الوقت الحالي"
- أو تمرير كلمة alif بالإضافة إلى اسم الملف test.alif حيث سيتم تنفيذ الشفرة المكتوبة في الملف test


---



### اوامر الطرفية التي يمكن تنفيذها:
1. ##### النسخة الحالية:
	```
	alif -v
	```

1. ##### المساعدة:
	```
	alif -h
	```


### العمليات التي تستطيع اللغة تنفيذها:

1. ##### التعليقات التوضيحية "#":
    ```
	س = 9 # هذا تعليق يوضح عمل الشفرة المكتوبة
	# تعليق في سطر فارغ
	```

2. ##### حالات متعددة في سطر:
```
س = 5؛ ص = 7
اطبع(س، ص)
```

3. ##### العمليات الحسابية:  
   ```
   جمع: 1 + 2 = 3
   طرح: 9 - 3 = 6
   ضرب: 2 * 8 = 16
   قسمة: 9 \ 3 = 3
   باقي القسمة: 8 \\ 5 = 3
   ناتج القسمة: 8 \* 3 = 2
   قوة: 4 ^ 2 = 16
   جذر: \^ 4 = 2
   أولوية: (3 + 6) * 2 = 18
   ```

3. ##### المقارنة:
    ```
	يساوي: 9 == 9
	لا يساوي: 9 != 9
	اكبر من: 3 > 6
	اصغر من: 6 < 9
	اكبر من او يساوي: 6 >= 6
	اصغر من او يساوي: 9 <= 3
	
	```
	
4. ##### العمليات المنطقية:
    ```
	و: صح و صح
	او: صح او صح
	ليس: ليس خطا 
	```
5. ##### الإسناد:
	```
	س = 9
	س, ص = 9, 7
	
	```
6. ##### إسناد رجعي:
	```
	س += 5
	س -= 3
	س *= 2
	س \= 7
	س ^= 3
	س \*= 7
	س \\= 9
	```
	
7. ##### إسناد الشرطي:
    ```
	س = 9 اذا 6 == 6 والا 7  
	```
	حيث س تساوي 9 اذا تحقق الشرط 
	وفي حال لم يتحقق الشرط س تساوي 7.

8. ##### الحذف:
	```
	س = 5
	احذف س
	```
9. ##### الدوال الضمنية:
    ```
	ادخل("نص هنا: ")
	اطبع("نص هنا او متغير")
	اقصى(2, 9)
	ادنى(3, 7)
	طول(س)  !! حيث س مصفوفة او مترابطة او فهرس
	```
	الدوال الضمنية هي مجموعة من الدوال المضمنة في اللغة نفسها، ومع ذلك من الممكن كتابة دالة فوقها.  

10. ##### النصوص:
	```
	س = "هذا نص عادي"
	س = م"هذا نص منسق حيث يمكن تمرير عمليات فيه من خلال {7 + 9} القوسين المتعرجين"
	```
	كما نلاحظ حيث أنه يوجد نص عادي ونص منسق والذي يقبل تمرير حالات بسيطة ضمن القوسين المتعرجين
	   
1. ##### مصفوفة:  
    ```
	س = [5, "سلام", صح]
	س.اضف(7)
	س.امسح("سلام")
	س.ادرج(0, 99)
	س[2]
	س = [9 لاجل _ في مدى(9)]
	```
	حيث 
	س.اضف(7) يقوم بإضافة عدد 7 الى نهاية المصفوفة
	س.امسح("سلام") يقوم بمسح او حذف العنصر "سلام" من المصفوفة
	س.ادرج(0, 99) يقوم بإدراج العدد 99 في موقع المؤشر 0 أي اول عنصر
	س[2] الوصول لقيمة العنصر الثالث.
	س = [9 لاجل _ في مدى(9)] هذه الحالة تسمى المصفوفة الضمنية حيث يتم إنشاء مصفوفة عن طريق حالة تكرار
	خاصية التكرار خلال المصفوفة, حيث يتم التكرار عبر القيم.	
	
	
11. ##### مترابطة:
	```
	س = 3, 5, 7
	س = (5, 7, 9)
	```
	خاصية التكرار خلال المترابطة, حيث يتم التكرار عبر القيم.
12. ##### فهرس:
	```
	س = {"أ": 5, "ب": 7, "ت": 9}
	س.مفاتيح()
	```
	حيث س.مفاتيح() تقوم بإرجاع مفاتيح الفهرس.
	خاصية التكرار خلال الفهرس, حيث يتم التكرار عبر المفاتيح وليس القيم.
	
13. ##### الاستيراد:
	```
	استورد مثال
	من مثال استورد ترحيب
	```
	حيث يمكن استيراد ملفات ذات لاحقة .aliflib فقط
	عندما يتم استيراد مكتبة يتم تنفيذها وعند إعادة استيرادها لا يتم تنفيذها مرة اخرى
	
	- الاستيراد من الحزم
	```
	مكتبة\        المستوى-الاعلى للحزمة
		|----	_تهيئة_.aliflib
		|---- ملف_مكتبة.aliflib
		|---- فرعية\
			|----	_تهيئة_.aliflib
			|---- ملف_فرعي.aliflib
	```
	
	```
	استورد مكتبة.فرعية.ملف_فرعي
	من مكتبة.فرعية استورد ملف_فرعي
	من مكتبة.فرعية.ملف_فرعي استورد تم_الاستيراد  !! حيث أن تم_الاستيراد هي عبارة عن دالة ضمن ملف_فرعي
	استورد مكتبة.فرعية  !! بشرط أن يتم استيراد دالة تم_الاستيراد في ملف _تهيئة_ الخاص بذلك المجلد "أي مجلد فرعية"
	```
	نفس الحالات تطبق على مجلد مكتبة وملف_مكتبة
	
14. ##### النطاق:
	```
	عدد = 1
	دالة زيادة(س):
		عام عدد
		عدد += س
	```
	حالة "عام" تستخدم لجعل المتغير عدد عام ضمن نطاق الدالة بحيث يمكن إستخدامه ضمنها - وحيث أن العدد لن يصبح عام في حال عدم استدعاء الدالة
	
15. ##### المكتبات الضمنية:
	المكتبات الضمنية وهي المكتبات الداخلية الخاصة بلغة ألف
	```
	الوقت
	الوقت.الوقت()  !! حيث يقوم بإحضار الوقت الحالي بالثواني
	الوقت.غفوة(9)   !! حيث يأخذ الممر الحالي غفوة لمدة 9 ثواني
	
	الرياضيات
	الرياضيات.جيب(30)
	الرياضيات.تجيب(60)
	الرياضيات.ظل(90)  !! النتيجة تحسب بالراديان
	```
	
16. ##### حالة "اذا":
    ```
	س = 6
	اذا س > 9:
    	اطبع("س أكبر من 9")
	اواذا س == 9:
    	اطبع("س تساوي 9") 
	والا:
    	اطبع("س ليست اكبر من 9 ولا تساويها")
	```
17. ##### حالة "لاجل":
    ```
	لاجل ب في مدى(10):
    	اطبع(ب)
	```
	في هذا المثال يتم عمل حلقة "لاجل" لتنفيذ برنامج يطبع قيمة ب .  
	حيث تبدأ الحلقة بالعدد 0 وتقوم بإسناده إلى المتغير ب ومن ثم تنفذ جسم الحلقة.  
	يتكرر الأمر حتى وصول الحلقة إلى العدد 9 - وهو العاشر لأن الحلقة بدأت من 0 - وبالتالي يتوقف التنفيذ.
    ```
    لاجل ب في مدى(2,100,20):
    	اطبع(ب)
	```
	سيتم إسناد 20 إلى المتغير ب ومن ثم يقوم بتنفيذ الحلقة.  
	بعد الإنتهاء سيتم إسناد 22 إلى المتغير ب وذلك لأن عدد التزايد المحدد هو 2.   
	وهكذا حتى يصل المتغير ب الى العدد 100.  
    ```
    لاجل ب في ("سلام", 1 , خطأ):
		اطبع(ب)
	```
	حيث هنا سيقوم بطباعة القيم الموجودة ضمن المترابطة.

18. ##### حالة "بينما":
    ```
	س = 1
	بينما س < 10:
		اذا س == 4:
			س += 3
			استمر
		اواذا س == 9:
			توقف
		اطبع(س)
		س += 1
	```
	سيتم التحقق من الشرط (س < 10) ، إذا كان صحيحاً سيتم تنفيذ جسم الحالة.  
	اذا كانت س تساوي 4 فإن استمر لن تكمل باقي الجسم وسيعود للتحقق من س في بداية الحالة بينما
	واذا كانت س تساوي 9 سيقوم بالتوقف عن التكرار والخروج من الحالة بينما

19. ##### حالة حاول:
	```
	حاول:
		اطبع("محاولة")
		س = 1 + "س"
	خلل:
		اطبع("خلل")
	وإلا:
		اطبع("لا يوجد خلل")
	نهاية:
		اطبع("نهاية حالة حاول")
	```
	حيث سيتم طباعة "محاولة" وبسبب وجود خلل سيقوم بطباعة "خلل" وبعد ذلك سيقوم بطباعة "نهاية حالة حاول" حيث هذه الحالة يتم تنفيذها مهما كان قبلها
	
20. ##### تعريف دالة:
	```
	دالة السرعة(المسافة, الزمن = 1):
        ارجع المسافة \ الزمن
	
	سرعة_سقوط_الكرة = السرعة(10, 5)
	
	اطبع(سرعة_سقوط_الكرة)
	```
	في هذا المثال تم توضيح تعريف الدالة مع تمرير معاملان أحدهما لا يمتلك قيمة افتراضية والآخر يمتلك قيمة افتراضية.  
	كما أنه تم توضيح طريقة إرجاع قيمة من الدالة.  
	والمثال عبارة عن دالة لحساب السرعة في حال تحديد قيمة المسافة المقطوعة والزمن.  
	وفي حال عدم تحديد قيمة الزمن فإن الدالة ستقوم بإستخدام القيمة الإفتراضية الممررة.
	```
	دالة السرعة(ش = 6):
		اطبع(ش)
		لاجل ص في (5, 7, 9):
			اذا ص == 9:
				توقف
		ارجع ص
	
	اطبع(السرعة(7))
	```

21. ##### تعريف صنف: 
    ```
	صنف مخروط_علوي:
		دالة _تهيئة_(هذا, نق = 1, ع = 1):	
			هذا.نق_القاعدة = نق
			هذا.الارتفاع = ع
			هذا.بي = 3.14
	
		دالة مساحة_القاعدة(هذا):
			م_القاعدة = 2 * هذا.بي * هذا.نق_القاعدة * هذا.الارتفاع
			ارجع م_القاعدة
	
		دالة حجم_المخروط(هذا):
			ارجع هذا.مساحة_القاعدة() * هذا.الارتفاع \ 3
	
	صنف مخروط_سفلي:
		دالة _تهيئة_(هذا, نق = 1, ع = 1, مخروط_ع = عدم):	
			هذا.نق_القاعدة = نق
			هذا.الارتفاع = ع
			هذا.بي = 3.14
			هذا.مخروط_ع = مخروط_ع
	
		دالة مساحة_القاعدة(هذا):
			م_القاعدة = 2 * هذا.بي * هذا.نق_القاعدة * هذا.الارتفاع
			ارجع م_القاعدة
	
		دالة حجم_المخروط(هذا):
			ارجع هذا.مساحة_القاعدة() * هذا.الارتفاع \ 3
	
		دالة مح_المخروطين(هذا):
			ارجع هذا.حجم_المخروط() + هذا.مخروط_ع.حجم_المخروط()
		
	مخروط_ع = مخروط_علوي(2, 2)
	مخروط_س = مخروط_سفلي(3, 4, مخروط_ع)
	
	ح_المخاريط= مخروط_س.مح_المخروطين()
	
	اطبع(مخروط_ع.حجم_المخروط(), مخروط_س.حجم_المخروط(), ح_المخاريط)
	```
	في هذا المثال تم تعريف صنف من نوع مخروط وتم تمرير صنف ككائن ضمن صنف اخر.
	تتم تهيئة الصنف عن طريق دالة خاصة تدعى \_تهيئة\_
	
	###### الوراثة في الاصناف:
	```
	صنف انسان:
		دالة _تهيئة_(هذا, عمر):
			هذا.العمر = عمر
	
		دالة اظهر(هذا):
			اطبع(هذا.العمر)

	صنف شخص(انسان):
		دالة _تهيئة_(هذا, عمر, طول):
			اصل()._تهيئة_(عمر)
			هذا.الطول = طول
	
	الشخص = شخص(40, 180)
	
	الشخص.اظهر()
	```
	
	حيث تتم الوراثة عن طريق تمرير اسم الصنف الموروث ضمن الصنف الوارث, ويتم تهيئة الصنف الموروث عن طريق دالة اصل()._تهيئة_(معاملات) والتي تقوم بإستدعاء الدالة الاصل لتهيئتها,
	في حال الحاجة لاستيراد اكثر من صنف يتم ذلك عن طريق (موروث1, موروث2) وتتم تهيئتها عن طريق اسم الموروث بدل من اصل() كالتالي:
		موروث1._تهيئة_(هذا, معاملات)
		موروث2._تهيئة_(هذا, معاملات)
	
22. ##### تحويل النوع:
	```
	س = صحيح("1")
	س = عشري("0.5")
	س = مصفوفة(فهرس.مفاتيح())
	```
23. ##### حالات خاصة:
	```
	س = عشري("لانهائي")
	س = عشري("-لانهائي")
	```
	حيث الاولى ترجع قيمة عدد لانهائي موجب والثانية ترجع قيمة عدد لانهائي سالب
	*************************************************************
	
	 حالة مقارنة خاصة:
	```
	س = 7 في [3, 2, 9]
	س = 7 ليس في [4, 8, 3]
	```
	حيث الحالة الاولى ترجع خطأ لان 7 ليست في المصفوفة والثانية ترجع صح لان 7 ليست في المصفوفة 
	

: ــــــــــــــــــــــــــــــــــــــــــــــــــــــــــــــــــــــــــــــــــــــــــــــــــــــــــــــــــــــــــــــــــــــــــــــــ :

| ملاحظات: |
| :---: |
| محدد الخطأ غير مكتمل، وقد يتم تحديد مكان الخطأ بدقة أو تظهر رسالة توضح أنه يوجد خطأ وقد يتم الخروج من البرنامج دون معرفة السبب أو الخطأ. |
| لغة ألف5 تقوم بإدارة الذاكرة بشكل تلقائي، ولكن يفضل عدم معالجة كميات ضخمة جداً من البيانات أو الدخول في حلقات لا نهائية وذلك تفادياً لأي ضرر قد يحصل لجهازك. |
